'---そうこゲーム
GOSUB @INITGAME
WHILE 1
 GOSUB @INITGAME2
 MESSAGE " STAGE "+STR$(STAGE)+" "
 WHILE 1
  GOSUB @PLAYER
  'VSYNC 16
  FOR I=0 TO 15
   C$=INKEY$() 'for KEY REPEAT
   VSYNC 1
  NEXT
  GOSUB @STATUS
  IF (GCNT==BCNT)OR(GIVEUP==TRUE) THEN BREAK
 WEND
 IF GCNT==BCNT THEN
  'BGMPLAY 4
  BEEP 5
  MESSAGE " CLEAR! "
  STAGE=STAGE+1
  IF STAGE>4 THEN BREAK
 ELSE
  'BGMPLAY 5
  BEEP 3
  SPANIM AN, "I", 1, 2516, 1
  MESSAGE " GIVE UP "
 ENDIF
WEND
ACLS
MESSAGE " ALL CLEAR!!! "
END
'---しょきか
@INITGAME
ACLS
VISIBLE 1,0,1
GIVEUP=TRUE
ID_WALL=1  '1:かべ
ID_BOX=2   '2:はこ
ID_START=3 '3:スタート
ID_GOAL=4  '4:ゴール
DIM X1[4],Y1[4], MYCH[4]
DIM SPCH[5]
FOR I=0 TO 3
 READ X1[I],Y1[I],MYCH[I]
NEXT
DATA  1,0 ,2544   '0:みぎ
DATA  0,1 ,2548   '1:した
DATA -1,0 ,2552   '2:ひだり
DATA  0,-1,2556   '3:うえ
FOR I=1 TO 4
 READ SPCH[I]
NEXT
DATA 2290  '1:かべ
DATA 2317  '2:はこ
DATA 2548  '3:スタート
DATA 2346  '4:ゴール
RETURN
'---しょきか2
@INITGAME2
IF GIVEUP==TRUE THEN
 GOSUB @TITLE
 GIVEUP=FALSE
 STAGE=1
ENDIF
ACLS:WAIT 20
'LAY=0
'BGSCREEN LAY,25,15
RESTORE "@MAP"+FORMAT$("%02D",STAGE)
HABA=16    '1マスのいどうりょう
SPN=0   'スプライトばんごう
FOR TY=0 TO 14
 READ A$
 IF A$=="" THEN BREAK
 FOR TX=0 TO LEN(A$)-1
  'BGPUT LAY,TX,TY,571
  ID=VAL(MID$(A$,TX,1))
  IF ID==0 THEN CONTINUE
  X=(TX*HABA)+(HABA/2)
  Y=(TY*HABA)+HABA
  Z=-ID
  SPSET SPN,SPCH[ID]
  SPOFS SPN, X, Y, Z
  SPHOME SPN,HABA/2,HABA-1
  SPVAR SPN,0,ID
  IF ID==ID_START THEN AN=SPN 'スプライトばんごう
  SPN=SPN+1
 NEXT
NEXT
SPMAX=SPN  'ぜんスプライトすう
RETURN
'---
@PLAYER
T=15
WHILE 1
 VSYNC 1
 BT=BUTTON()
 'IF BT==#RIGHT THEN MUKI=0:BREAK
 'IF BT==#DOWN  THEN MUKI=1:BREAK
 'IF BT==#LEFT  THEN MUKI=2:BREAK
 'IF BT==#UP    THEN MUKI=3:BREAK
 'IF BT==#X     THEN GIVEUP=TRUE:RETURN
 IF BT==(1<<#EMUJOY_RIGHT) THEN MUKI=0:BREAK
 IF BT==(1<<#EMUJOY_DOWN)  THEN MUKI=1:BREAK
 IF BT==(1<<#EMUJOY_LEFT)  THEN MUKI=2:BREAK
 IF BT==(1<<#EMUJOY_UP)    THEN MUKI=3:BREAK
 IF BT==(1<<#EMUJOY_2)     THEN GIVEUP=TRUE:RETURN
 C$=INKEY$()
 'DIAMOND CURSOR
 IF C$=="D" THEN MUKI=0:BREAK
 IF C$=="X" THEN MUKI=1:BREAK
 IF C$=="S" THEN MUKI=2:BREAK
 IF C$=="E" THEN MUKI=3:BREAK
 'ZEPLIS
 IF C$=="K" THEN MUKI=0:BREAK
 IF C$=="M" THEN MUKI=1:BREAK
 IF C$=="H" THEN MUKI=2:BREAK
 IF C$=="U" THEN MUKI=3:BREAK
 'TEN KEY
 IF C$=="6" THEN MUKI=0:BREAK
 IF C$=="2" THEN MUKI=1:BREAK
 IF C$=="4" THEN MUKI=2:BREAK
 IF C$=="8" THEN MUKI=3:BREAK
 'GIVEUP
 IF C$==CHR$(13) OR C$==CHR$(13) THEN GIVEUP=TRUE:RETURN
WEND
'BEEP 59
BEEP 4
SPOFS AN OUT AX,AY
AX1=AX+(X1[MUKI]*HABA)
AY1=AY+(Y1[MUKI]*HABA)
ID1=SPGETID(AX1,AY1)
IF ID1==ID_WALL THEN RETURN
IF ID1==ID_BOX THEN
 AX2=AX1+(X1[MUKI]*HABA)
 AY2=AY1+(Y1[MUKI]*HABA)
 ID2=SPGETID(AX2,AY2)
 IF ID2==ID_BOX OR ID2==ID_WALL THEN RETURN
 'IF ID2==ID_GOAL THEN BEEP 7
 IF ID2==ID_GOAL THEN BEEP 2
 'BEEP 40
 SPANIM BOXSPN,"XY",1,AX1,AY1,-T,AX2,AY2,1
ENDIF
SPANIM AN,"XY",1,AX,AY,-T,AX1,AY1,1
C=MYCH[MUKI]
SPANIM AN,"I",T/4,C,T/4,C+1,T/4,C+2,T/4,C+3,1
RETURN
'--- スプライトをさがす
DEF SPGETID(X,Y)
 ID=999
 FOR SPN=0 TO SPMAX-1
  SPOFS SPN OUT TX,TY
  IF ABS(X-TX)<HABA AND ABS(Y-TY)<HABA THEN
   NEWID=SPVAR(SPN,0)
   IF NEWID==ID_BOX THEN BOXSPN=SPN
   IF ID>NEWID THEN ID=NEWID
  ENDIF
 NEXT
 RETURN ID
END
'--—メッセージ
DEF MESSAGE A$
VISIBLE 1,0,0
TX=25-(LEN(A$)/2):TY= 14
COLOR #YELLOW,#MAROON:LOCATE TX,TY:PRINT A$
WAIT 120
COLOR #WHITE, 0: LOCATE TX,TY:PRINT " "*LEN(A$)
VISIBLE 1,0,1
END 
'---ステータス
@STATUS
BCNT=0   'かんりょうしたかず
GCNT=0   'ゴールのかず
FOR I=0 TO SPMAX-1
 IF SPVAR(I,0)==ID_GOAL THEN
  GCNT=GCNT+1
  SPOFS I OUT X,Y
  IF SPGETID(X,Y)==ID_BOX THEN BCNT=BCNT+1
 ENDIF
NEXT
COLOR #WHITE,#BLACK
LOCATE 18,26
PRINT "GOAL ";BCNT; "/";GCNT;
RETURN
'---タイトル
@TITLE
ACLS
COLOR #WHITE,#PURPLE
LOCATE 17, 8:PRINT "  SOUKO GAME   "
LOCATE 16,19:PRINT " PUSH ANY BUTTON "
COLOR #WHITE,#BLACK
WHILE(BUTTON()==0 AND INKEY$()==""):VSYNC 1:WEND
RETURN
'---マップ
@MAP01
DATA "111111111111"
DATA "111        1" 
DATA "1 3 2    4 1"
DATA "1  12    4 1"
DATA "1          1"
DATA "111111111111"
DATA "
@MAP02
DATA "111111111111"
DATA "1          1"
DATA "1  4 1 2   1"
DATA "1 3        1"
DATA "1   21     1"
DATA "1      4   1"
DATA "1  2 1  4  1"
DATA "1          1"
DATA "111111111111"
DATA ""
@MAP03
DATA "111111111111"
DATA "11   1111111"
DATA "1 21 4    11"
DATA "1  111112  1"
DATA "1   44  2  1"
DATA "1 2 11     1"
DATA "11   4   111"
DATA "111  1 3  11"
DATA "111111111111"
DATA ""
@MAP04
DATA "1111111111111111111111"
DATA "1            111    11"
DATA "1  111  22           1"
DATA "1   1  1 111 111 4   1"
DATA "1 3     2    1114444 1"
DATA "1  211 1 111     4   1"
DATA "1 22   1     111    11"
DATA "11     1 1  1111     1"
DATA "1111111111111111111111"
DATA ""

